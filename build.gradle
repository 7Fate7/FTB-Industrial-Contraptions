plugins {
	id "dev.architectury.loom" version "0.10.0-SNAPSHOT"
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
	silentMojangMappingsLicense()

	forge {
		mixinConfigs = [
		]

		dataGen {
			mod project.mod_id
		}

		launches {
			data {
				arg "--existing", file("src/main/resources").absolutePath
				// arg "--assetIndex", "1.16"
				// arg "--assetsDir", (ENV.GRADLE_USER_HOME ?: "${System.getProperty("user.home")}/.gradle") + "/caches/fabric-loom/assets"
			}
		}
	}
}

repositories {
	maven {
		url "https://maven.architectury.dev/"
		content {
			includeGroup "me.shedaniel"
			includeGroup "me.shedaniel.cloth"
		}
	}

	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url "https://maven.nanite.dev/releases"
	}

	maven {
		url "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.ftb.mods"
			includeGroup "io.alwa.mods"
		}
	}

	maven {
		url "https://maven.ftb.dev"
		content {
			includeGroup "io.alwa.mods"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings minecraft.officialMojangMappings()
	forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
	modImplementation("me.shedaniel:architectury-forge:${rootProject.architectury_version}")
	// modImplementation("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}") { transitive = false }
	modCompileOnly("curse.maven:jei-238222:${jei_file}")
	modImplementation("com.ridanisaurus:emendatusenigmatica:${rootProject.ee_version}")
	modImplementation("io.alwa.mods:myrtrees-forge:${rootProject.myrtrees_version}")
	modImplementation("dev.latvian.mods:rhino-forge:${rootProject.rhino_version}")
	modImplementation("dev.latvian.mods:kubejs-forge:${rootProject.kubejs_version}") { transitive = false }
	modRuntime("curse.maven:ctm-267602:3137659")
	modImplementation("dev.ftb.mods:ftb-library-forge:${rootProject.ftblibrary_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-teams-forge:${rootProject.ftbteams_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-chunks-forge:${rootProject.ftbchunks_version}") { transitive = false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}

	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			artifact(remapJar) {
				builtBy remapJar
				classifier null
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/release"
				credentials {
					username = "ftb"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "ftb"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = project.curseforge_type
			addGameVersion "1.16.5"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-forge'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}